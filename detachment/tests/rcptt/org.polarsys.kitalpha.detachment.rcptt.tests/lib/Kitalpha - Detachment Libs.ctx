--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Kitalpha - Detachment Libs
Element-Type: context
Element-Version: 2.0
Id: _Iot8AOoJEeWM8Nol5H2vPg
Runtime-Version: 2.0.0.201506120617
Save-Time: 3/14/16 6:21 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

//Launch a model detachment on the model specified in the path. Path must include project name
proc detachment_launchModelDetachment [val path] {
	get-view "Project Explorer" | get-tree 
    | select $path
    | get-menu "Model Detachment" | click
}

//Activate tab name in form editor
//The editor must be opened before
proc detachment_setVisibleTabItemInEditor [val editor] [val tabName]{
	get-editor $editor | get-tab-folder | get-tab-item $tabName | click
}

//activate tab name in detachment editor
//The detachment editor must be opened before
proc detachment_setVisibleTabItem [val tabName]{
	detachment_setVisibleTabItemInEditor "Detachment" $tabName
}

// Uncheck vpName to detach
proc detachment_prepareToDetachViewpoint [val vpName] [val nsURI ""] [val nbVpDetected 0] {
	detachment_setVisibleTabItem -tabName "Viewpoints Tree Page"
	
	get-editor "Detachment" | get-section "Viewpoints Hierarchy" | get-tree | get-item $vpName | get-property childCount | equals $nbVpDetected | verify-true
	
	get-section "Viewpoints Hierarchy" | get-tree | get-item $vpName | uncheck
	with [get-section "Viewpoints Hierarchy" | get-tree] {
//		get-property "getData().getUriToRemove().size()" | equals 1 | verify-true
//		get-property "getData().getUriToRemove().toString()" 
//        | equals $nsURI | verify-true
    	get-item $vpName | get-property checked | equals false | verify-true
	}
}

//Perform the detachment
proc detachment_performDetachment {
	get-editor "Detachment" | get-button "Perform Detachment" | click
	get-window "Warning..." | get-button Yes | click
	get-window "Detachment..." | get-button OK | click
}

proc detachment_openSession [val path] {
	get-view "Project Explorer" | get-tree 
    | select $path
    | get-menu "Open"
    | click
}

proc detachment_closeSession [val path] {
	get-view "Project Explorer" | get-tree 
    | select $path
    | get-menu "Close"
    | click
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
